import com.github.triplet.gradle.androidpublisher.ReleaseStatus

plugins {
    id 'com.android.application'
    id 'com.github.triplet.play' version '3.12.1'
}

android {
    namespace "com.applikationsprogramvara.osmviewer"
    compileSdk 36

    defaultConfig {
        applicationId "com.applikationsprogramvara.osmviewer"
        minSdkVersion 21
        targetSdkVersion 36
        versionCode 14
        versionName "1.8.1 25-10-26"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    signingConfigs {
        release {
            def keystorePropertiesFile = rootProject.file("keystore.properties")
            if (keystorePropertiesFile.exists()) {
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

                keyAlias keystoreProperties['releaseKeyAlias']
                keyPassword keystoreProperties['releaseKeyPassword']
                storeFile file(rootDir.getCanonicalPath() + '/' + keystoreProperties['releaseKeyStore'])
                storePassword keystoreProperties['releaseStorePassword']
            }

        }
    }

    buildTypes {
        debug {

        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    namespace 'com.applikationsprogramvara.osmviewer'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.7.1' // upgrading to 1.7.0 require to raise SDK to 21, the last version before 1.7.0 is 1.6.1
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.9.4' // keeping 2.5.1 to prevent duplicate class error 2.7.0 -> 19

    implementation 'org.osmdroid:osmdroid-android:6.1.20'
    implementation 'com.github.MKergall:osmbonuspack:6.9.0'

    implementation 'androidx.preference:preference:1.2.1'

    implementation "androidx.multidex:multidex:2.0.1"


    if (rootProject.file("osmviewer_addon").exists()) {
        debugImplementation project(':osmviewer_addon')
    }
}

// how to publish:
// 1. put keystore.properties and service-key.json to the project root folder
// 2. bump versionCode and versionName, update app/src/main/play/release-notes/en-US/beta.txt
// 3. use gradle icon to call gradle start tool and use task publishBundle
play {
    track = 'beta' // alpha / beta / production
    releaseName = android.defaultConfig.versionName
    releaseStatus = ReleaseStatus.DRAFT
    defaultToAppBundles = true
    serviceAccountCredentials = file("../service-key.json")
}
