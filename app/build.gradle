plugins {
    id 'com.android.application'
    id 'com.github.triplet.play' version '3.6.0'
}

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.applikationsprogramvara.osmviewer"
        minSdkVersion 16
        targetSdkVersion 33
        versionCode 12
        versionName "1.7 23-02-09"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            def keystorePropertiesFile = rootProject.file("keystore.properties")
            if (keystorePropertiesFile.exists()) {
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

                keyAlias keystoreProperties['releaseKeyAlias']
                keyPassword keystoreProperties['releaseKeyPassword']
                storeFile file(rootDir.getCanonicalPath() + '/' + keystoreProperties['releaseKeyStore'])
                storePassword keystoreProperties['releaseStorePassword']
            }

        }
    }

    buildTypes {
        debug {

        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    namespace 'com.applikationsprogramvara.osmviewer'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1' // to prevent duplicate class error

    implementation 'org.osmdroid:osmdroid-android:6.1.13'
    implementation 'com.github.MKergall:osmbonuspack:6.6.0'

    implementation 'com.jakewharton:butterknife:10.2.3'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.3'

    implementation 'androidx.preference:preference:1.2.0'

    if (rootProject.file("osmviewer_addon").exists()) {
        debugImplementation project(':osmviewer_addon')
    }
}

// put keystore.properties and service-key.json to the project root folder
// use gradle icon to call gradle start tool and use task publishBundle
play {
    track = 'beta' // alpha / beta / production
    releaseName = android.defaultConfig.versionName
    releaseStatus = com.github.triplet.gradle.androidpublisher.ReleaseStatus.DRAFT
    defaultToAppBundles = true
    serviceAccountCredentials = file("../service-key.json")
}
